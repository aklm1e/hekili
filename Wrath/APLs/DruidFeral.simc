# APL v3.4.0-1.3.0 #
actions.init+=/variable,name=leeway_time,value=(buff.dire_bear_form.up&action.mangle_bear.gcd|buff.cat_form.up&action.mangle_cat.gcd|action.gift_of_the_wild.gcd)>?latency
actions.init+=/variable,name=fight_end,value=query_time+target.time_to_die
actions.init+=/variable,name=furor_cap,value=20*talent.furor.rank<?85
actions.init+=/call_action_list,name=tf_now
actions.init+=/call_action_list,name=berserk_now
actions.init+=/call_action_list,name=roar_now
actions.init+=/call_action_list,name=rip_now
actions.init+=/call_action_list,name=rake_now
actions.init+=/call_action_list,name=mangle_now
actions.init+=/call_action_list,name=bite_now
actions.init+=/call_action_list,name=bearweave_now
actions.init+=/call_action_list,name=emergency_bearweave
actions.init+=/call_action_list,name=shred_now
actions.init+=/call_action_list,name=cat_now
actions.init+=/call_action_list,name=powerbear_now

actions.tf_now+=/variable,name=tf_energy_thresh,value=40-10*variable.leeway_time
actions.tf_now+=/variable,name=next_possible_lac,value=leeway_time+3.5+latency
actions.tf_now+=/variable,name=tf_now,value=energy.current<variable.tf_energy_thresh&cooldown.tigers_fury.up&!buff.berserk.up&buff.cat_form.up
actions.tf_now+=/variable,name=tf_now,op=setif,value=variable.tf_now&(!debuff.lacerate.up|debuff.lacerate.remains>variable.next_possible_lac|debuff.lacerate.remains>target.time_to_die),if=bearweaving_lacerate_enabled
actions.berserk_now+=/variable,name=wait_for_tf,value=cooldown.tigers_fury.remains<=buff.berserk.duration&query_time+cooldown.tigers_fury.remains+1<variable.fight_end-buff.berserk.duration
actions.berserk_now+=/variable,name=berserk_now,value=cooldown.berserk.up&!variable.wait_for_tf
actions.roar_now+=/variable,name=roar_now,value=combo_points.current>=1&(!buff.savage_roar.up|clip_roar)
actions.rip_now+=/variable,name=rip_refresh_pending,value=debuff.rip.up&debuff.rip.remains<target.time_to_die-end_thresh
actions.rip_now+=/variable,name=rip_now,value=combo_points.current=5&!debuff.rip.up&target.time_to_die>=end_thresh&!buff.clearcasting.up
actions.rake_now+=/variable,name=rake_now,value=!debuff.rake.up&target.time_to_die>9&!buff.clearcasting.up&rake_over_shred
actions.mangle_now+=/variable,name=mangle_now,value=!variable.rip_now&!debuff.mangle.up
actions.bite_now+=/variable,name=can_bite,value=debuff.rip.remains>=min_bite_rip_remains&buff.savage_roar.remains>=min_bite_sr_remains
actions.bite_now+=/variable,name=bite_before_rip,value=combo_points.current=5&debuff.rip.up&buff.savage_roar.up&ferociousbite_enabled&variable.can_bite
actions.bite_now+=/variable,name=bite_at_end,value=combo_points.current=5&(target.time_to_die<end_thresh|(debuff.rip.up&target.time_to_die-debuff.rip.remains<end_thresh))
actions.bite_now+=/variable,name=bite_now,value=(bite_before_rip|bite_at_end)&!buff.clearcasting.up
actions.bite_now+=/variable,name=bite_now,op=setif,value=energy.current<=max_bite_energy,if=variable.bite_now&buff.berserk.up
actions.bearweave_now+=/variable,name=weave_energy,value=variable.furor_cap-30-20*latency
actions.bearweave_now+=/variable,name=weave_energy,op=setif,value=variable.weave_energy-15,if=talent.furor.rank>3
actions.bearweave_now+=/variable,name=weave_energy,op=setif,value=variable.weave_energy-15,if=bearweaving_lacerate_enabled&!debuff.lacerate.up
actions.bearweave_now+=/variable,name=energy_to_dump,value=energy.current+weave_end*10
actions.bearweave_now+=/variable,name=post_weave_seconds,value=variable.energy_to_dump/42
actions.bearweave_now+=/variable,name=post_weave_seconds,op=floor
actions.bearweave_now+=/variable,name=bearweave_now,value=bearweaving_enabled&energy.current<variable.weave_energy&!buff.clearcasting.up&(!variable.rip_refresh_pending|debuff.rip.remains>=weave_end)&!buff.berserk.up
actions.bearweave_now+=/variable,name=bearweave_now,op=setif,value=!tf_expected_before_weave_end,if=variable.bearweave_now&!bearweaving_lacerate_enabled
actions.bearweave_now+=/variable,name=bearweave_now,op=setif,value=weave_end+variable.post_weave_seconds<target.time_to_die
actions.emergency_bearweave+=/variable,name=emergency_bearweave,value=bearweaving_lacerate_enabled&debuff.lacerate.up&debuff.lacerate.remains<2.5+latency&debuff.lacerate.remains<target.time_to_die&!buff.berserk.up
actions.shred_now+=/variable,name=shred_now,value=excess_e>=action.shred.spend|buff.clearcasting.up|energy.current>100-latency
actions.cat_now+=/variable,name=cat_next,value=energy.current+30+10*latency>variable.furor_cap|(variable.rip_refresh_pending&debuff.rip.remains<4.5)|buff.berserk.up
actions.cat_now+=/variable,name=cat_now,value=energy.current+15+10*latency>variable.furor_cap|(variable.rip_refresh_pending&debuff.rip.remains<3)|buff.berserk.up
actions.lacerate_now+=/variable,name=build_lacerate,value=!debuff.lacerate.up|debuff.lacerate.stacks<5
actions.lacerate_now+=/variable,name=maintain_lacerate,value=!build_lacerate&debuff.lacerate.remains<8&(rage.current<38|variable.cat_next)&debuff.lacerate.remains<target.time_to_die
actions.lacerate_now+=/variable,name=lacerate_now,value=bearweaving_lacerate_enabled&(build_lacerate|maintain_lacerate)

actions.precombat+=/mark_of_the_wild,if=!up&!buff.gift_of_the_wild.up
actions.precombat+=/thorns,if=!up
actions.precombat+=/cat_form,if=!up
actions.precombat+=/potion

actions+=/call_action_list,name=init
actions+=/use_items
actions+=/potion
actions+=/dire_bear_form,if=emergency_bearweave
actions+=/faerie_fire_feral,if=!debuff.armor_reduction.up&target.outside2
actions+=/tigers_fury,use_off_gcd=1,if=variable.tf_now
actions+=/berserk,if=variable.berserk_now
actions+=/pool_resource,for_next=1
actions+=/savage_roar,if=variable.roar_now
actions+=/pool_resource,for_next=1
actions+=/rip,if=variable.rip_now
actions+=/pool_resource,for_next=1
actions+=/ferocious_bite,if=variable.bite_now
actions+=/pool_resource,for_next=1
actions+=/rake,if=variable.rake_now
actions+=/pool_resource,for_next=1
actions+=/mangle_cat,if=variable.mangle_now
actions+=/dire_bear_form,if=variable.bearweave_now
actions+=/shred,if=variable.shred_now

actions.bearweaving+=/cat_form,if=cat_now